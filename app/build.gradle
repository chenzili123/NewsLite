apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.klz.news"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 5
        versionName "1.0.5.${releaseTime()}"
        manifestPlaceholders = [K_CHANNEL_VALUE: "klz"]
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {

        }
        release {
            storeFile file("E:/WhereU/WhereU.jks")
            if (System.console() != null)
                storePassword System.console().readLine("\nKeystore password: ")
            keyAlias "news"
            if (System.console() != null)
                keyPassword System.console().readLine("\nKey password: ")
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true

            // 移除无用的resource文件
            shrinkResources false
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为app_v1.0_2015-xx-xx_xxxx.apk
                        //def fileName = "app_v${defaultConfig.versionName}_${releaseTime()}.apk"
                        def fileName = "app_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        klz {}
        sumsang {}
        xiaomi {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [K_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile 'com.alibaba:fastjson:1.2.21'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.jcodecraeer:xrecyclerview:1.3.2'
    testCompile 'junit:junit:4.12'
    compile 'org.lucee:commons-lang:2.6.0'
}
// 指导文章
// http://www.cnblogs.com/permanent2012moira/p/5126276.html

// 打包
// Terminal里面直接输入：gradle assembleRelease