apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.klz.news"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 12
        versionName "1.1.0.${releaseTime()}"
        manifestPlaceholders = [K_CHANNEL_VALUE: "klz"]
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
        }
        release {
            storeFile file("E:/WhereU/WhereU.jks")
            if (System.console() != null){
                storePassword System.console().readLine("\nKeystore password: ")
            }
            keyAlias "news"
            if (System.console() != null){
                keyPassword System.console().readLine("\nKey password: ")
            }
    }
}

buildTypes {
    debug {
        // 显示Log
        buildConfigField "boolean", "LOG_DEBUG", "true"
        versionNameSuffix "-debug"
        minifyEnabled false
        zipAlignEnabled false
        shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true

            // 移除无用的resource文件
            shrinkResources false
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all{
                    outputFileName = "app_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                }
            }
        }
    }
    flavorDimensions "brand"
    productFlavors {
        klz {
            dimension "brand"
        }
        sumsang {
            dimension "brand"
        }
        xiaomi {
            dimension "brand"
        }
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [K_CHANNEL_VALUE: name]
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:support-v13:23.4.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jcodecraeer:xrecyclerview:1.3.2'
    compile 'org.lucee:commons-lang:2.6.0'
    compile 'com.alibaba:fastjson:1.2.21'
    compile 'org.xutils:xutils:3.3.40'
}

// 指导文章
// 混淆 http://www.cnblogs.com/permanent2012moira/p/5126276.html
// 打包 http://blog.csdn.net/shineflowers/article/details/45042485

// 打包--需要配置gradle的环境变量（path=>C:\Users\VULCAN\.gradle\gradle-3.3\bin）,选对应的版本（C:\Users\VULCAN\.gradle\wrapper\dists）复制到前面的.gradle里面，然后重启studio
// Terminal里面直接输入：gradle assembleRelease
// 没有看到提交信息呀....